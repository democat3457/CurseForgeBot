plugins {
    id 'java'
    id 'application'
}

version = '1.2.6'
group = "de.erdbeerbaerlp.curseforgeBot" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
mainClassName = 'de.erdbeerbaerlp.curseforgeBot.Main'

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('net.dv8tion:JDA:4.2.1_253'){
        transitive = true
        exclude module: 'opus-java'
    }
    
    embed('com.github.TheRandomLabs:CurseAPI:master-SNAPSHOT'){
        transitive = true
    }
    embed('com.github.TheRandomLabs:CurseAPI-Minecraft:master-SNAPSHOT'){
        transitive = true
    }
    
    embed 'com.typesafe:config:1.4.1'
    embed 'es.nitaur.markdown:txtmark:0.16'
    embed files('libs/stringtree-json-2.0.9.jar')
    
    embed 'commons-cli:commons-cli:1.4'
    embed 'commons-io:commons-io:2.8.0'
    embed('org.kohsuke:github-api:1.128'){
        exclude module: 'okhttp'
    }
}

jar {
    configurations.embed.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
} 
